use ir::*;

grammar;

pub Input: Input = {
    Comment* <universal_regions:UniversalRegions>  <var_uses_region:VarUsesRegion?>  <var_drops_region:VarDropsRegion?> Comment* <blocks:BlockDefn*> => Input::new(<>)
};

Comment: () = {
    r"//.*"
};

VarRegionMappings = Comma<VarRegionMapping>;
VarRegionMapping: (String, String) = {
                 "(" <Variable> "," <Region> ")" => (<>),
};

VarUsesRegion = "var_uses_region" "{" <VarRegionMappings> "}";
VarDropsRegion = "var_drops_region" "{" <VarRegionMappings> "}";
UniversalRegions = "universal_regions" "{" <Comma<Region>> "}";
BlockDefn : Block = {
    "block" <name:Block> "{" <statements:Statement*> Comment* <goto:Goto> "}" => Block { <> },
};

Goto: Vec<String> = {
    "goto" <Comma<Block>> ";",
    () => Vec::new(),
};

Statement : Statement = {
    Comment* <Effects> ";" => Statement::new(<>),
    Comment* <start_effects:Effects> "/" <effects:Effects> ";" => Statement::with_start_effects(<>),
};

Effects = Comma<Effect>;
Effect = {
    <Fact> => Effect::Fact(<>),
    Use
};

Fact : Fact = {
  "outlives" "(" <a:Region> ":" <b:Region> ")" => Fact::Outlives { <> },
  "borrow_region_at" "(" <region:Region> "," <loan:Loan> ")" => Fact::BorrowRegionAt { <> },
  "invalidates" "(" <loan:Loan> ")" => Fact::Invalidates { <> },
  "kill" "(" <loan:Loan> ")" => Fact::Kill { <> },
  "var_used" "(" <variable:Variable> ")" => Fact::UseVariable { <> },
  "var_defined" "(" <variable:Variable> ")" => Fact::DefineVariable { <> },
  "region_live_at" "(" <region:Region> ")" => Fact::RegionLiveAt { <> },
  "var_drop_used" "(" <variable:Variable> ")" => Fact::UseVariable { <> },
};

Use : Effect = "use" "(" <regions:Comma<Region>> ")" => Effect::Use { <> };

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Region: String = {
    r"'\w+" => <>.to_string()
};

Block: String = {
    r"B\w+" => <>.to_string()
};

Loan: String = {
    r"L\w+" => <>.to_string()
};

Variable: String = {
    r"V\w+" => <>.to_string()
};

